# vim: set ft=zsh sw=2 ts=2 fdm=marker:
# UTILS {{{
function is_remote () {
  [[ -n "$SSH_CLIENT" ]] || [[ -n "$SSH_TTY" ]]
}

function is_mac () {
  [[ "$(uname -a)" =~ ".*Darwin.*" ]]
}

function is_linux () {
  [[ "$(uname)" == "Linux" ]]
}

function is_ubuntu () {
  [[ "$(uname -a)" =~ ".*Ubuntu.*" ]]
}

function is_arch () {
  [[ "$(uname -a)" =~ ".*arch.*" ]]
}

function is_docker_container () {
  [[ -f "/.dockerenv" ]]
}
# }}}

# TMUX {{{
function tmux_start_or_attach () {
  if [[ -x "$(command -v tmux)" ]]; then
    if [[ -z "${TMUX}" ]] ;then
      ID="`tmux ls | grep -vm1 attached | cut -d: -f1`"
      if [[ -z "${ID}" ]] ;then
        tmux -2 new-session
      else
        tmux -2 attach-session -t "${ID}"
      fi
    fi
  fi
}
# }}}

# OH-MY-ZSH PLUGINS {{{
function set_zsh_plugins () {
  plugins=(git dotenv fzf gitignore sudo virtualenv virtualenvwrapper)
  is_mac && plugins+=(osx brew brew-cask)
}
# }}}

# SOURCING {{{
function source_other_files () {
  local fzf_paths=("${HOME}/.fzf.zsh"
  "/usr/share/fzf/key-bindings.zsh"
  "/usr/share/completion.zsh")

  for fzf_path in "${fzf_paths[@]}"; do
    if [[ -f "${fzf_path}" ]]; then
      source "${fzf_path}"
      break
    fi
  done

  [[ -f "${ZSH}/oh-my-zsh.sh" ]] && source "${ZSH}/oh-my-zsh.sh"
  [[ -s "/etc/grc.zsh" ]] && source /etc/grc.zsh
  [[ -f "${HOME}/.zsh_aliases" ]] && source ${HOME}/.zsh_aliases
  [[ -f "${HOME}/.zsh_functions" ]] && source ${HOME}/.zsh_functions
}
# }}}

# ENV VARS {{{
function set_env_vars () {
  export VISUAL=vim
  export EDITOR="${VISUAL}"
  export BROWSER="firefox"
  is_remote && export REMOTE_CONNECTION="true"
}

# }}}

# PATH {{{
function set_user_path () {
  typeset -U path
  path+=("${HOME}/.local/bin"
  "${HOME}/bin")

  _go="/usr/local/go/bin/go"
  if [[ -x "${_go}" ]]; then
    path+=("$(dirname ${_go})"
    "$(${_go} env GOPATH)/bin")
    export GOPATH="$(${_go} env GOPATH)"
  fi

  _cargo_bin="${HOME}/.cargo/bin"
  if [[ -d "${_cargo_bin}" ]]; then
    path+=("${_cargo_bin}")
  fi
  export PATH
}
# }}}

# ALIASES {{{
function set_aliases () {
  if which gvim > /dev/null; then
    alias vim='gvim -v'
  fi
  alias watch='watch --color'
  alias ssh='TERM=xterm-256color ssh'
  alias cdr='pushd $(git rev-parse --show-toplevel)'
  alias groot='echo $(git rev-parse --show-toplevel)'
  alias unneeded='echo **/(*.pyc|*.pyo|__pycache__|tags|*.retry)'
  alias grep='grep  --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn}'
  if is_mac; then
    alias ls='grc gls --color=always'
  else
    alias ls='grc ls --color=always'
  fi
  # open files in vim automatically
  alias -s {yml,yaml,md}=vim
}
# }}}

# PYTHON {{{
function setup_virtualenvwrapper () {
  export VIRTUAL_ENV_DISABLE_PROMPT=1
  export WORKON_HOME="$HOME/python"
  export PROJECT_HOME=$HOME/repos
  local venv_wrapper="${HOME}/.local/bin/virtualenvwrapper.sh"
  if [[ -f "${venv_wrapper}" ]]; then
    source "${venv_wrapper}"
  fi
}
# }}}

function main () {
  ZSH=${HOME}/.oh-my-zsh
  set_env_vars
  set_user_path
  set_zsh_plugins
  source_other_files
  setup_virtualenvwrapper
  set_aliases
  (( $(id -u) == 0 )) || tmux_start_or_attach
  ZSH_THEME="wisp"
}

main
